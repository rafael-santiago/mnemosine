#
#                          Copyright (C) 2019 by Rafael Santiago
#
# Use of this source code is governed by GPL-v2 license that can
# be found in the COPYING file.
#

include ~/toolsets/gcc/gcc-lib.hsl
include ~/toolsets/clang/clang-lib.hsl
include ~/toolsets/common/utils/lang/c/dependency_scanner.hsl

local var toolset_name type string;
local var deps type string;
local var src type list;
local var inc type list;
local var cflags type list;
local var lib type list;
local var ldflags type list;

project libmnemosine : toolset $toolset_name : dependencies $deps : $src, $inc, $cflags, $lib, $ldflags, "libmnemosine.a";

libmnemosine.preloading() {
    $toolset_name = "gcc-c-lib";

    var option type list;

    $option = hefesto.sys.get_option("toolset");

    if ($option.count() > 0) {
        var prefix type string;
        $prefix = $option.item(0);
        $toolset_name.replace("^gcc", $prefix);
    }
}

libmnemosine.prologue() {
    $inc = hefesto.sys.get_option("includes");
    $cflags = hefesto.sys.get_option("cflags");
    $lib = hefesto.sys.get_option("libraries");
    $ldflags = hefesto.sys.get_option("ldflags");
    $src.ls(".*\\.c$");
    $deps = get_c_cpp_deps();
}

libmnemosine.epilogue() {
    if (hefesto.sys.last_forge_result() == 0) {
        hefesto.sys.echo("\n*** BUILD SUCCESS.\n");
        if (run_tests() != 0) {
            hefesto.project.abort(1);
        }
    } else {
        hefesto.sys.echo("\n~~~ BUILD ERROR.\n");
    }
}

local function run_tests() : result type int {
    var exit_code type int;
    var old_cwd type string;
    $old_cwd = hefesto.sys.pwd();
    $exit_code = 1;
    if (hefesto.sys.cd("test")) {
        var cmdline type string;
        var name type string;
        $name = $toolset_name;
        $name.replace("-.*$", "");
        $cmdline = "hefesto --toolset=" + $name;
        $exit_code = hefesto.sys.run($cmdline);
        hefesto.sys.cd($old_cwd);
    } else {
        hefesto.sys.echo("\n~~~ BUILD EXCEPTION: Unable to find test directory.\n");
    }
    result $exit_code;
}
